#!/usr/bin/perl
use warnings;
use strict;

my $width_mult = 3;
my $height_mult = 4;

my $dwidth;			# per char.
my $dest_dwidth;

while (<>) {
    if (m{^ \s* FONT \s+
	  -([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)
	  -([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)-([^-]*)}xi) {
	my ($foundry, $family, $weight, $slant, $width, $style, $pixel, $point, $res_x, $res_y, $spacing, $avgwidth, $registry, $encoding) = ($1, $2, $3, $4, $5, $6, $7, $8, $9, $10, $11, $12, $13, $14);
	$pixel *= $height_mult;
	$point *= $height_mult;
	if (lc($weight) eq "medium") {
	    $weight = "semicondensed";
	    $avgwidth = int($avgwidth * $width_mult / $height_mult + 0.5);
	}
	print("FONT -${foundry}-${family}-${weight}-${slant}-${width}-${style}-${pixel}-${point}-${res_x}-${res_y}-${spacing}-${avgwidth}-${registry}-${encoding}\n");
    }
    elsif (s{^( \s* size \s+ )(-?\d+)}{$1 . ($2 * $height_mult)}exi) {
	print;
    }
    elsif (s{^( \s* pixel_size \s+ )(-?\d+)}{$1 . ($2 * $height_mult)}exi) {
	print;
    }
    elsif (s{^( \s* average_width \s+ )(-?\d+)}{$1 . int($2 * $width_mult / $height_mult + 0.5)}exi) {
	print;
    }
    elsif (s{^( \s* (?:cap|x)_height \s+ )(-?\d+)}{$1 . ($2 * $height_mult)}exi) {
	print;
    }
    elsif (s{^( \s* font_(?:descent|ascent) \s+ )(-?\d+)}{$1 . ($2 * $height_mult)}exi) {
	print;
    }
    elsif (s{^( \s* quad_width \s+ )(-?\d+)}{$1 . ($2 * $width_mult)}exi) {
	print;
    }
    elsif (s{^( \s* swidth \s+ )(-?\d+)}{$1 . int($2 * $width_mult / $height_mult + 0.5)}exi) {
	print;
    }
    elsif (s{^( \s* dwidth \s+ )(-?\d+)}{$1 . ($2 * $width_mult)}exi) {
	$dwidth = $2;
	$dest_dwidth = $dwidth * $width_mult;
	print;
    }
    elsif (s{^ \s* bbx \s+ (-?\d+) \s+ (-?\d+) \s+ (-?\d+) \s+ (-?\d+)}
	    {sprintf("BBX %d %d %d %d", $1 * $width_mult, $2 * $height_mult, $3 * $width_mult, $4 * $height_mult)}exi) {
	print;
    }
    elsif (s{^ \s* fontboundingbox \s+ (-?\d+) \s+ (-?\d+) \s+ (-?\d+) \s+ (-?\d+)}
	    {sprintf("FONTBOUNDINGBOX %d %d %d %d", $1 * $width_mult, $2 * $height_mult, $3 * $width_mult, $4 * $height_mult)}exi) {
	print;
    }
    elsif (m{^\s*bitmap\b}xi) {
	print;
	while (<>) {
	    if (m{^\s*endchar\b}xi) {
		print;
		last;
	    }
	    chomp();
	    s{\r}{}g;

	    my $bits = pack("H" . length($_), $_);
	    $bits = unpack("B" . $dwidth, $bits);
	    my @bits = split('', $bits);
	    $bits = join("", map { $_ . $_ . $_ } @bits);
	    $bits = substr($bits, 0, $dest_dwidth);
	    $bits = pack(("B" . $dest_dwidth), $bits);
	    $bits = unpack("H" . int(($dest_dwidth + 3) / 4), $bits);
	    my $length = length($bits);
	    my $hex2 = hex($bits);
	    $hex2 = $hex2 | $hex2 >> 1 | $hex2 << 1;
	    my $bits2 = sprintf("%0" . $length . "X", $hex2);

	    print(("0" x length($bits)), "\n");
	    print("$bits\n");
	    foreach (2 .. $height_mult - 2) {
		print("$bits2\n");
	    }
	    print("$bits\n");
	}
    }
    else {
	print;
    }
}

